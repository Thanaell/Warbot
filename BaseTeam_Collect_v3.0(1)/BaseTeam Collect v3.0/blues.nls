;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;;
;; le code de l'equipe verte basique
;; préfixe : blue-team
;;
;; mem0 et mem1 : coordonnees d'une cible
;; mem5 : indique si le robot a ou non une cible sélectionnée
;; mem4 : indique si un harvester est en mode retour à la base
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

to blue-team-ask-for-energy [ b n ]
  ;; TWEAKME seuil de générosité de la base
  if (ticks < 3000) [
    ifelse (breed = Harvesters)
      [ ask b [ if (energy > 4000) [ give-energy myself n ]] ]
      [ ask b [ if (energy > 12000) [ give-energy myself n ]] ]
  ]
  if (ticks >= 3000) and (ticks < 6000) [
    ifelse (breed = Harvesters)
      [ ask b [ if (energy > 4000) [ give-energy myself n ]] ]
      [ ask b [ if (energy > 10000) [ give-energy myself n ]] ]
  ]
  if (ticks >= 6000) [
    ifelse (breed = Harvesters)
      [ ask b [ if (energy > 6000) [ give-energy myself n ]] ]
      [ ask b [ if (energy > 10000) [ give-energy myself n ]] ]
  ]
end

;;
;; rentre à la base si le robot transporte plus de 1000 unités de nourriture ou si son énergie est inférieure à 500 ou si c'est un rocket-launcher qui n'a plus de missiles / fafs
;;
to blue-team-go-back-to-base
  ;; détermine la base la plus proche
  let b min-one-of my-bases [ distance myself ]
  ;; BUG CORRIGE
  if (b != nobody) [
    ;; si le robot est arrivé à la base
    ifelse (distance b <= 2)
      [ 
        ;; dépose sa nourriture
        give-food b carrying-food?
        
        ;; TWEAKME toutes les valeurs d'énergies, seuils, nb de missiles / fafs
        if (Breed = RocketLaunchers) [
          if (energy < 1000) [ blue-team-ask-for-energy b (4000 - energy) ]
          if (nb-fafs < 2) [
            blue-team-ask-for-energy b (rocket-launcher-max-fafs * faf-cost)
            new-faf rocket-launcher-max-fafs
          ]
          if (nb-missiles = 0) [
            blue-team-ask-for-energy b (100 * missile-cost)
            new-missile 100
          ]
        ]
        if (Breed = Harvesters) [
          if (energy < 500) [
            ifelse (mem3 = 42)
              [ blue-team-ask-for-energy b (2000 - energy) ]
              [ blue-team-ask-for-energy b (1000 - energy) ]
          ]
        ]
        if (Breed = Explorers) [
          if (energy < 1000) [ blue-team-ask-for-energy b (1500 - energy) ]
        ]
        set mem4 0
        ;; fait demi-tour
        rt 180
      ]
    ;; sinon
      [
        ;; s'oriente vers la base avec un peu d'aléatoire
        set heading towards b - 20 + random 40
        ;; fait un demi tour si il y a du monde devant
        ifelse (free-ahead? speed = nobody)  [ forward-move speed ][ rt random 360 if (free-ahead? 2 = nobody) [ forward-move speed ]]
      ]
    ]    
end

to blue-team-go-and-eat
  if ((breed = RocketLaunchers and (mem5 = 0)) or (breed = Explorers)) [
    random-move
  ]
end

to blue-team-harvesters-go-and-eat
  let b min-one-of perceive-food [ distance myself ]
  ifelse (b != nobody) [ 
    ifelse (distance b) <= 2
      [ take b ]
      [ set heading towards b - 20 + random 40
      ifelse (free-ahead? speed = nobody) [ forward-move speed ][ rt random 360 if (free-ahead? 2 = nobody) [ forward-move speed ]]
      ]
  ]
  [
    ifelse (mem5 = 1)
    [ 
      ifelse (distancexy mem0 mem1 > 1)
      [
        set heading towardsxy mem0 mem1
        ifelse (free-ahead? speed = nobody) [ forward-move speed ][ rt random 360 if (free-ahead? 2 = nobody) [ forward-move speed ]]
      ]
      [ set mem5 0 ]
    ]
    [ random-move ]
  ]
end

;;
;; la mémoire mem5 est à
;; - 0 si le robot n'a pas de cible sélectionnée
;; - 1 si le robot a une cible sélectionnée
;;
to-report blue-team-no-target?
  report mem5 = 0
end

to blue-team-set-target-xy [ x y espece ]
  set mem0 x set mem1 y set mem2 espece set mem5 1
end

to blue-team-set-target-t [ t espece ]
  set mem0 t set mem2 espece set mem5 2
end

to blue-team-set-food-target [ x y ]
  if (blue-team-no-target?) [ set mem0 x set mem1 y set mem5 1 ] 
end

to blue-team-call-rocket-launcher-xy [ x y espece ]
  let rl min-one-of perceive-specific-robots color RocketLaunchers [ distancexy x y ]
  if (rl != nobody) [ ask rl [ blue-team-set-target-xy x y espece ]]
end

to blue-team-call-rocket-launcher-t [ t espece ]
  let rl min-one-of perceive-specific-robots color RocketLaunchers [ distance t ]
  if (rl != nobody) [ ask rl [ blue-team-set-target-t t espece ]]
end

to blue-team-call-explorer [ x y espece ]
  let ex one-of perceive-specific-robots color Explorers
  if (ex != nobody) [
    ask ex [ blue-team-set-target-xy x y espece ]
  ]
end

to blue-team-call-harvester [ x y ]
  let h min-one-of perceive-specific-robots color harvesters [ distancexy x y ]
  if (h != nobody) [ ask h [ blue-team-set-food-target x y ]]
end

;;
;; essaye de sélectionner une cible à viser
;;
to blue-team-select-target
  ;; si le robot n'a pas une base adverse comme cible
  if (blue-team-no-target?)
  [
    ;; il essaye de percevoir un robot ennemi (le plus proche de lui)
    let h min-one-of perceive-robots2 ennemy1 ennemy2 [ distance myself ]
    ;; si il en a vu un, il mémorise ses coordonnées et verrouille la cible
    ifelse ( h != nobody ) [
      set mem0 h
      set mem2 [breed] of h
      ;; mem5 à 2 pour signifier qu'on connait la position de la cible => faf possible
      set mem5 2
    ]
    ;; sinon, il se dévérouille
    [ set mem5 0 ]
  ]
end

to blue-team-shoot
  if (mem5 = 1)
  [ launch-rocket towardsxy mem0 mem1 set mem5 0 ]
  if (mem5 = 2)
  [ launch-faf mem0 set mem5 0 ]
end

to blue-team-drive-harvesters
  let food one-of perceive-food
  if (food != nobody) [
    blue-team-call-harvester [xcor] of food [ycor] of food 
  ]
end

to goBlueExplorer
  ;; TWEAKME seuil de retour à la base Explorer
  ifelse (energy < 500)
  [ blue-team-go-back-to-base ]
  [ blue-team-go-and-eat ]
  
  blue-team-drive-harvesters

  ifelse (not blue-team-no-target?) [
    blue-team-call-rocket-launcher-xy mem0 mem1 mem2
    blue-team-call-explorer mem0 mem1 mem2
  ]
  [ let h one-of perceive-robots2 ennemy1 ennemy2
    if ( h != nobody ) [ blue-team-call-rocket-launcher-t h [breed] of h]
  ]
end

to goBlueRocketLauncher
  ;; TWEAKME seuils de retour à la base RocketLauncher
  ifelse (energy < 1000) or (nb-missiles = 0) or (nb-fafs = 0)
  [
    ;; rentre à la base si certaines conditions sont vérifiées
    blue-team-go-back-to-base
  ]
  [
    ;; sinon sélectionne une cible
    blue-team-select-target
    ifelse (blue-team-no-target?)
    ;; si pas de cible, cherche à manger
    [ blue-team-go-and-eat ]
    ;; sinon tire
    [ blue-team-shoot ]
  ]
end

to goBlueHarvester
  ;;TWEAKME Proportion de farmers (1337) et de cueilleurs (42)
  if(mem3 != 42 and mem3 != 1337)[
    ifelse(random 3 = 0)[set mem3 42][set mem3 1337]
  ]
  ;; TWEAKME seuils de retour à la base Harvester
  ifelse (mem4 = 1) or (mem3 = 42 and carrying-food? > 1500) or (mem3 = 1337 and carrying-food? > 1000) or (energy < 400)
  [
    ; retour à la base
    set mem4 1
    ;; rentre à la base si certaines conditions sont vérifiées
    blue-team-go-back-to-base
    ;; TWEAKME seuils d'envie soudaine d'agriculture
    if (energy > 500) and (carrying-food? > 500) [ 
      ;; détermine la distance de la base la plus proche
      ;; BUG CORRIGE
      let b min-one-of my-bases [ distance myself ]
      if (b != nobody) [
         ;; TWEAKME distance max de la base pour planter
        let maxdistance 0
        ifelse(ticks < 3000)[set maxdistance 14]
        [ifelse (ticks < 6000)[set maxdistance 16][set maxdistance 18]] 
        if (distance b < maxdistance)
          [ plant-seeds color max-seeds ]
      ]
    ]
  ]
  [
    ;; cherche à manger
    blue-team-harvesters-go-and-eat
  ]
end

to goBlueBase
  ;; TWEAKME tous les seuils et valeurs de construction
  if (nb-fafs < 12) and (energy > 5000) [ new-faf 6 ]
  
  ;; Variation de stratégie selon le nombre de ticks
  if (ticks < 3000) [
    if (energy > 8000) [ 
       new-Harvester 1 self
    ]
  ]
  if (ticks >= 3000 and ticks < 6000) [
    if (energy > 12000) [
      ;; Répartition :  2/5 harvester 3/5 rocketLauncher
      let result random 5
      ifelse (result = 0 or result = 1)
        [ new-Harvester 1 self ]
        [ new-RocketLauncher 1 self ]
    ]
  ]
  if (ticks >= 6000) [
    if (energy > 12000) [
      ;; Répartition : 3/6 rocketlauncher 1/6 explorer 2/6 harvester
      let randomResult (random 6)
      ifelse (randomResult < 3)
        [ new-RocketLauncher 1 self ]
        [ ifelse (randomResult < 4) [ new-Explorer 1 self ][ new-Harvester 1 self ] ]
    ]
  ]
  let t one-of perceive-specific-robots2 ennemy1 ennemy2 rocketlaunchers
    if (t != nobody) [
      if (random 10 = 0) [launch-rocket towards t]
    ]
end

;; procedure pour initialiser les explorers verts
to initBlueExplorer
end

;; procedure pour initialiser les rocket-launchers verts
to initBlueRocketLauncher
end

;; procedure pour initialiser les harvesters verts
to initBlueHarvester
end

;; procedure pour initialiser les bases verts
to initBlueBase
  ;; TWEAKME spawns de départ
  ;; crée des explorers, des harvesters et des rocket-launchers
  new-Explorer 2 self
  new-Harvester 8 self
  new-RocketLauncher 2 self
end
